// Обработка проведения документа "Потребление".
// Формирует движения расхода по регистру "Продукты" с контролем остатков и расчётом стоимости по средней.
Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	// Проверка: не проводим пустой документ
	Если Продукты.Количество() = 0 Тогда
		Сообщить("Родной, документ строк то не содержит");
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	// Включаем запись движений по регистру накопления "Продукты"
	Движения.Продукты.Записывать = Истина;
	
	// Запрос в два шага:
	// 1) Временная таблица втДанныеДокумента — списание по документу (продукт, суммарное количество, представление)
	// 2) Соединение с остатками на момент времени: проверка достаточности остатков и расчёт стоимости (средняя цена)
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ПотреблениеПродукты.Продукт КАК Продукт,
		|	СУММА(ПотреблениеПродукты.Колличество) КАК Количество,
		|	ПотреблениеПродукты.Продукт.Представление КАК ПродуктПредставление
		|ПОМЕСТИТЬ втДанныеДокумента
		|ИЗ
		|	Документ.Потребление.Продукты КАК ПотреблениеПродукты
		|ГДЕ
		|	ПотреблениеПродукты.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	ПотреблениеПродукты.Продукт
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втДанныеДокумента.Продукт КАК Продукт,
		|	втДанныеДокумента.Количество КАК Количество,
		|	ВЫБОР
		|		КОГДА ЕСТЬNULL(ПродуктыОстатки.КоличествоОстаток, 0) >= втДанныеДокумента.Количество
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК ДостаточноОстатков,
		|	ВЫБОР
		|		КОГДА втДанныеДокумента.Количество = ЕСТЬNULL(ПродуктыОстатки.КоличествоОстаток, 0)
		|			ТОГДА ЕСТЬNULL(ПродуктыОстатки.СуммаОстаток, 0)
		|		ИНАЧЕ ВЫБОР
		|				КОГДА ЕСТЬNULL(ПродуктыОстатки.КоличествоОстаток, 0) = 0
		|					ТОГДА 0
		|				ИНАЧЕ ЕСТЬNULL(ПродуктыОстатки.СуммаОстаток, 0) / ЕСТЬNULL(ПродуктыОстатки.КоличествоОстаток, 0) * втДанныеДокумента.Количество
		|			КОНЕЦ
		|	КОНЕЦ КАК Стоимость
		|ИЗ
		|	втДанныеДокумента КАК втДанныеДокумента
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.Продукты.Остатки(
		|				&МоментВремени,
		|				Продукт В
		|					(ВЫБРАТЬ
		|						втДанныеДокумента.Продукт КАК Продукт
		|					ИЗ
		|						втДанныеДокумента КАК втДанныеДокумента)) КАК ПродуктыОстатки
		|		ПО втДанныеДокумента.Продукт = ПродуктыОстатки.Продукт";
	
	// Параметры запроса: момент времени для остатков и ссылка на текущий документ
	Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	// Первый проход: проверка достаточности остатков по каждому продукту
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Если НЕ ВыборкаДетальныеЗаписи.ДостаточноОстатков Тогда
			Сообщить("Родной, остатки не достаточны для проведения документа" + ВыборкаДетальныеЗаписи.ПродуктПредставление);
			Отказ = Истина;
			Возврат;
		КонецЕсли;
	КонецЦикла;
	
	// Второй проход: формирование движений расхода (только если контроль остатков пройден)
	Если НЕ Отказ Тогда
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			Движение = Движения.Продукты.Добавить();
			Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
			Движение.Период = Дата;
			Движение.Продукт = ВыборкаДетальныеЗаписи.Продукт;
			Движение.Количество = ВыборкаДетальныеЗаписи.Количество;
			// Стоимость рассчитана в запросе по средней (СуммаОстаток/КоличествоОстаток * списание)
			Движение.Сумма = ВыборкаДетальныеЗаписи.Стоимость;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры